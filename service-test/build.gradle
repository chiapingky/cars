import net.masterthought.cucumber.ReportBuilder
import org.apache.commons.io.FileUtils

buildscript {
    dependencies {
        classpath "net.masterthought:cucumber-reporting:${cucumber_reporting_version}"
    }
}

plugins {
    id 'java'
}


dependencies {
    testImplementation "com.intuit.karate:karate-junit5:${karate_version}"
    testImplementation "net.masterthought:cucumber-reporting:${cucumber_reporting_version}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junit_jupiter_version}"
}

sourceSets {
    test {
        resources {
            srcDir file('src/test/java')
            exclude '**/*.java'
        }
    }
}

tasks.register('testReport') {
    doLast {
        println 'Generating Karate reports...'

        def file_ext = ['json'] as String[]
        Collection<File> jsonFiles = FileUtils.listFiles(new File("$buildDir/karate-reports"), file_ext, true)
        List<String> jsonPaths = new ArrayList(jsonFiles.size())
        jsonFiles.each { file -> jsonPaths.add(file.getAbsolutePath()) }
        net.masterthought.cucumber.Configuration config = new net.masterthought.cucumber.Configuration(new File("$buildDir"), 'BOILERPLATE_CHANGE_ME')
        ReportBuilder reportBuilder = new ReportBuilder(jsonPaths, config)
        reportBuilder.generateReports()
        println "Report is generated at file://$config.reportDirectory.absolutePath/cucumber-html-reports/overview-features.html"
    }
}

test {
    useJUnitPlatform()
    finalizedBy(testReport)
}

repositories {
    mavenCentral()
    mavenLocal()
}